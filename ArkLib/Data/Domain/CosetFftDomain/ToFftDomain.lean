/-
Copyright (c) 2024-2026 ArkLib Contributors. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Julian Sutherland, Ilia Vlasov, Aristotle (Harmonic)
-/

import Mathlib.GroupTheory.SpecificGroups.Cyclic
import Mathlib.Algebra.Group.Fin.Basic
import Mathlib.Algebra.Group.TypeTags.Basic
import Mathlib.Algebra.Group.Defs
import Mathlib.Tactic.Cases
import Mathlib.Tactic.Linarith
import Mathlib.Tactic.LinearCombination
import Mathlib.Tactic.Field

import ArkLib.Data.Domain.CosetFftDomain.Mem
import ArkLib.Data.Domain.FftDomain.Mem

/-!
# Converting a coset FFT domain to an FFT domain

We define `toFftDomain`, normalizing a coset FFT domain `ω` into an `FftDomain` (coset generator
set to one). The evaluation bridge `eval_toFftDomain` and the membership characterization
`mem_toFftDomain_iff_mul_mem` relate the two domains, with `mul_mem` corollaries and the
finset-image / cardinality lemmas (`toFinset_image_toFftDomain_eq_toFinset`,
`card_toFinset_eq_card_toFftDomain_toFinset`) describing the image set.
-/

namespace Domain

variable {ι : Type} [AddCommGroup ι]
variable {F : Type} [Field F]

namespace CosetFftDomainClass

variable {D : Type} [FunLike D ι F] [CosetFftDomainClass D ι F]

def toFftDomain (ω : D) : FftDomain ι F where
  subgroupDomain := (CosetFftDomainClass.toCosetFftDomain ω).subgroupDomain
  subgroupDomain_inj := (CosetFftDomainClass.toCosetFftDomain ω).subgroupDomain_inj
  cosetGenerator := 1
  cosetGenerator_one := by rfl

lemma eval_toFftDomain {ω : D} {i : ι} :
    toFftDomain ω i = (ω 0)⁻¹ * ω i := by
  aesop (add
    simp [
      toFftDomain,
      FftDomain.eval_fft_domain_eq_eval_coset_fft_domain,
      CosetFftDomain.eval_coset_fft_domain_eq_eval_generator_mul_domain,
      toCosetFftDomain, mkSubgroupUnit])

lemma mem_toFftDomain_iff_mul_mem {ω : D} {x : F} :
    x ∈ toFftDomain ω ↔ ω 0 * x ∈ ω := by
  unfold toFftDomain
  rw [FftDomain.mem_iff_mem_toCosetFftDomain,
      CosetFftDomain.mem_iff_exists_mul]
  aesop
    (add simp
      [CosetFftDomainClass.mem_def,
       CosetFftDomainClass.toCosetFftDomain,
       CosetFftDomainClass.mkSubgroupUnit])
    (add safe (by field_simp))

lemma mul_mem_of_mem_toFftDomain_of_mem {ω : D} {x y : F}
    (hx : x ∈ toFftDomain ω)
  (hy : y ∈ ω) :
  x * y ∈ ω := by
  simp_all only
    [FftDomain.mem_iff_exists, Multiplicative.exists, toFftDomain, Multiplicative.ofAdd]
  obtain ⟨a, rfl⟩ := hy
  obtain ⟨b, rfl⟩ := hx
  simp only [Equiv.coe_fn_mk, toCosetFftDomain, mkSubgroupUnit,
              MonoidHom.coe_mk, OneHom.coe_mk]
  exists (a + b)
  have : a + b = Multiplicative.ofAdd a * Multiplicative.ofAdd b := by rfl
  rw [map_add]
  field_simp
  rfl

lemma mul_mem_of_mem_of_mem_toFftDomain {ω : D} {x y : F}
    (hx : x ∈ ω)
  (hy : y ∈ toFftDomain ω) :
  x * y ∈ ω := by
  rw [mul_comm]
  exact mul_mem_of_mem_toFftDomain_of_mem hy hx

@[simp]
lemma toFinset_image_toFftDomain_eq_toFinset [Fintype ι] [DecidableEq F] {ω : D} :
    Finset.image (fun (w : F) ↦ ω 0 * w) (toFftDomain ω).toFinset =
    CosetFftDomainClass.toFinset ω := by
  ext x
  constructor <;> rintro h
  · simp_all only [Finset.mem_image, mem_toFinset_iff_mem]
    obtain ⟨a, h₁, h₂⟩ := h
    rw [←h₂, mul_comm]
    exact mul_mem_of_mem_toFftDomain_of_mem h₁ (by simp)
  · simp_all only [mem_toFinset_iff_mem, Finset.mem_image]
    obtain ⟨a, h⟩ := h
    have : (ω 0)⁻¹ * x ∈ toFftDomain ω := by
      rw [mem_toFftDomain_iff_mul_mem]
      aesop
    exists _, this
    field_simp

lemma card_toFinset_eq_card_toFftDomain_toFinset [Fintype ι] [DecidableEq F] {ω : D} :
    Finset.card (toFinset ω) = Finset.card (toFftDomain ω).toFinset := by
  aesop (add simp [toFinset_image_toFftDomain_eq_toFinset])

end CosetFftDomainClass

namespace CosetFftDomain

variable {ω : CosetFftDomain ι F} {x : F}

abbrev toFftDomain (ω : CosetFftDomain ι F) : FftDomain ι F :=
  CosetFftDomainClass.toFftDomain ω

lemma mem_toFftDomain_iff_mul_mem :
    x ∈ ω.toFftDomain ↔ ω.cosetGenerator * x ∈ ω := by
  have : ω 0 = ω.cosetGenerator := by
    have : (0 : ι) = (1 : Multiplicative ι) := by rfl
    aesop (add simp [eval_coset_fft_domain_eq_eval_generator_mul_domain])
  aesop (add simp [CosetFftDomainClass.mem_toFftDomain_iff_mul_mem])

lemma mul_mem_of_mem_toFftDomain_of_mem {y : F}
    (hx : x ∈ ω.toFftDomain)
  (hy : y ∈ ω) :
  x * y ∈ ω := CosetFftDomainClass.mul_mem_of_mem_toFftDomain_of_mem hx hy

end CosetFftDomain

namespace FftDomain

lemma toFffDomain_eq_self {ω : FftDomain ι F} :
    ω.toFftDomain = ω := by
  ext i
  simp only [CosetFftDomainClass.toFftDomain,
    CosetFftDomainClass.toCosetFftDomain_of_CosetFftDomain,
    eval_fft_domain_eq_eval_coset_fft_domain,
    CosetFftDomain.eval_coset_fft_domain_eq_eval_generator_mul_domain, Units.val_one, one_mul,
    ne_eq, Units.ne_zero, not_false_eq_true, right_eq_mul₀, Units.val_eq_one]
  exact ω.cosetGenerator_one

end FftDomain

end Domain
