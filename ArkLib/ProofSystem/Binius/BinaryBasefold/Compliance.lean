/-
Copyright (c) 2025 ArkLib Contributors. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chung Thai Nguyen, Quang Dao
-/

import ArkLib.ProofSystem.Binius.BinaryBasefold.Code

/-!
## Binary Basefold Compliance

Protocol-level proximity, compliance, and bad-event definitions for Binary Basefold.
This file packages:
1. the compliance predicate relating adjacent folded codewords
2. fold-error containment and its UDR-close consequence
3. the full and incremental bad-event predicates used by the soundness development
-/

namespace Binius.BinaryBasefold

open OracleSpec OracleComp ProtocolSpec Finset AdditiveNTT Polynomial MvPolynomial
  Binius.BinaryBasefold
open scoped NNReal
open Function
open Finset AdditiveNTT Polynomial MvPolynomial Nat Matrix
open Code ReedSolomon BerlekampWelch ProbabilityTheory

noncomputable section SoundnessTools

variable {r : ℕ} [NeZero r]
variable {L : Type} [Field L] [Fintype L] [DecidableEq L] [CharP L 2]
variable (𝔽q : Type) [Field 𝔽q] [Fintype 𝔽q] [DecidableEq 𝔽q]
  [h_Fq_char_prime : Fact (Nat.Prime (ringChar 𝔽q))] [hF₂ : Fact (Fintype.card 𝔽q = 2)]
variable [Algebra 𝔽q L]
variable (β : Fin r → L) [hβ_lin_indep : Fact (LinearIndependent 𝔽q β)]
  [h_β₀_eq_1 : Fact (β 0 = 1)]
variable {ℓ 𝓡 ϑ : ℕ} (γ_repetitions : ℕ) [NeZero ℓ] [NeZero 𝓡] [NeZero ϑ]
variable {h_ℓ_add_R_rate : ℓ + 𝓡 < r}
variable {𝓑 : Fin 2 ↪ L}

/--
Compliance condition (Definition 4.18) : For an index `i` that is a multiple of `steps`,
the oracle `f_i` is compliant if it's close to the code fiber-wise, the next oracle
`f_i_plus_steps` is close to its code, and their unique closest codewords are consistent
with folding.
-/
def isCompliant (i : Fin r) {destIdx : Fin r} (steps : ℕ)
    (h_destIdx : destIdx = i + steps) (h_destIdx_le : destIdx ≤ ℓ) [NeZero steps]
  (f_i : OracleFunction 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate) i)
  (f_i_plus_steps : OracleFunction 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate) destIdx)
  (challenges : Fin steps → L) : Prop :=
  ∃ (h_fw_dist_lt : fiberwiseClose 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate) (i := i)
    (steps := steps) h_destIdx h_destIdx_le (f := f_i))
    (h_dist_next_lt : UDRClose 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
      (i := destIdx) (h_i := by omega) (f := f_i_plus_steps)),
    let h_dist_curr_lt := UDRClose_of_fiberwiseClose 𝔽q β
      (h_ℓ_add_R_rate := h_ℓ_add_R_rate) (i := i) steps h_destIdx h_destIdx_le f_i
      (h_fw_dist_lt := h_fw_dist_lt)
    let f_bar_i := UDRCodeword 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
      (i := i) (h_i := by omega) (f := f_i) (h_within_radius := h_dist_curr_lt)
    let f_bar_i_plus_steps := UDRCodeword 𝔽q β
      (h_ℓ_add_R_rate := h_ℓ_add_R_rate) (i := destIdx) (h_i := by omega)
      f_i_plus_steps h_dist_next_lt
    iterated_fold 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate) (steps := steps) (i := i)
      h_destIdx h_destIdx_le
      f_bar_i challenges = f_bar_i_plus_steps

/--
Farness implies non-compliance. If `f_i` is far from its code `C_i`, it cannot be
compliant. This follows directly from the contrapositive of
`fiberwise_dist_lt_imp_dist_lt`.
-/
lemma farness_implies_non_compliance (i : Fin r) {destIdx : Fin r} (steps : ℕ)
    (h_destIdx : destIdx = i + steps) (h_destIdx_le : destIdx ≤ ℓ) [NeZero steps]
  (f_i : OracleFunction 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate) i)
  (f_i_plus_steps : OracleFunction 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate) destIdx)
  (challenges : Fin steps → L)
  (h_far : 2 * Δ₀(f_i, (BBF_Code 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate) i))
    ≥ (BBF_CodeDistance 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate) (i := i) : ℕ∞)) :
  ¬ isCompliant 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate) (i := i) (steps := steps)
    (destIdx := destIdx) (h_destIdx := h_destIdx) (h_destIdx_le := h_destIdx_le)
    (f_i := f_i) (f_i_plus_steps := f_i_plus_steps) (challenges := challenges) := by
  intro h_compliant
  rcases h_compliant with ⟨h_fw_dist_lt, _, _⟩
  have h_close := UDRClose_of_fiberwiseClose 𝔽q β
    (h_ℓ_add_R_rate := h_ℓ_add_R_rate) (i := i) (steps := steps) (destIdx := destIdx)
    (h_destIdx := h_destIdx) (h_destIdx_le := h_destIdx_le) (f := f_i)
    (h_fw_dist_lt := h_fw_dist_lt)
  have h_not_far := LT.lt.not_ge h_close
  exact h_not_far h_far

/-- **Fold error containment**: Two words achieve `fold error containment` for a specific
tuple of challenges if folding them does not introduce new errors outside of their
fiberwise disagreement set. -/
def fold_error_containment (i : Fin r) {destIdx : Fin r} (steps : ℕ)
    (h_destIdx : destIdx = i + steps) (h_destIdx_le : destIdx ≤ ℓ)
    (f f_bar : OracleFunction 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate) i)
    (r_challenges : Fin steps → L) : Prop :=
  let folded_f := iterated_fold 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
    (i := i) (steps := steps) (destIdx := destIdx) (h_destIdx := by omega)
    (h_destIdx_le := h_destIdx_le) f r_challenges
  let folded_f_bar := iterated_fold 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
    (i := i) (steps := steps) (destIdx := destIdx) (h_destIdx := by omega)
    (h_destIdx_le := h_destIdx_le) f_bar r_challenges
  disagreementSet 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
    (i := destIdx) (destIdx := destIdx) (h_destIdx := rfl)
    (f := folded_f) (g := folded_f_bar) ⊆
  fiberwiseDisagreementSet 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
    (i := i) (steps := steps) (destIdx := destIdx)
    (h_destIdx := by omega) (h_destIdx_le := h_destIdx_le) f f_bar

/-! **Lemma 4.19.** For each `i ∈ {0, steps, ..., ℓ-steps}`, if `f⁽ⁱ⁾` is `UDR-close`, then, for
each tuple of folding challenges `(rᵢ', ..., r_{i+steps-1}') ∈ L^steps`, we have that
`fold error containment` holds.
-- * **Main Idea of Proof:** Proceeds by contraposition. If `y ∉ Δ⁽ⁱ⁾(f⁽ⁱ⁾, f̄⁽ⁱ⁾)`, then the
  restrictions of `f⁽ⁱ⁾` and `f̄⁽ⁱ⁾` to the fiber over `y` are identical. By Definition 4.8, this
  implies their folded values at `y` are also identical.
-- * **Intuition**: Because folding is local (Def 4.8), if `f⁽ⁱ⁾` and `f̄⁽ⁱ⁾` agree completely on
  the fiber above a point `y`, their folded values at `y` must also agree.
-- * **Consequence**: If `f⁽ⁱ⁾` is close to `f̄⁽ⁱ⁾`, then `fold(f⁽ⁱ⁾)` must be close to
  `fold(f̄⁽ⁱ⁾)`.
-/
lemma fold_error_containment_of_UDRClose (i : Fin r) {destIdx : Fin r} (steps : ℕ)
    (h_destIdx : destIdx = i + steps) (h_destIdx_le : destIdx ≤ ℓ) [NeZero steps]
  (f_i : OracleFunction 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate) i)
  (challenges : Fin steps → L)
  (h_UDRClose : UDRClose 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate) (i := i) (h_i := by omega)
    (f := f_i)) :
  let f_bar := UDRCodeword 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate) (i := i) (h_i := by omega)
    (f := f_i) (h_within_radius := h_UDRClose)
  fold_error_containment 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate) (i := i) (steps := steps)
    (destIdx := destIdx) (h_destIdx := h_destIdx) (h_destIdx_le := h_destIdx_le)
    (f := f_i) (f_bar := f_bar) (r_challenges := challenges) := by
  classical
  dsimp [fold_error_containment]
  intro y hy
  have h_steps_ne : steps ≠ 0 := NeZero.ne steps
  have h_exists : ∃ x, f_i x ≠
      UDRCodeword 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate) (i := i) (h_i := by omega)
        (f := f_i) (h_within_radius := h_UDRClose) x := by
    by_contra hnone
    have h_eq : f_i =
        UDRCodeword 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate) (i := i) (h_i := by omega)
          (f := f_i) (h_within_radius := h_UDRClose) := by
      funext x
      exact not_not.mp (by
        intro hx
        exact hnone ⟨x, hx⟩)
    -- Rewrite in the generalizing direction (composite `UDRCodeword …` term ↦ the variable
    -- `f_i`): the forward direction's motive is not type correct because the codeword's
    -- `h_within_radius` argument itself mentions `f_i`.  `simp only` rather than `rw`:
    -- the codeword occurrence in `hy` carries proof arguments that only match up to
    -- proof irrelevance, which `rw`'s keyed abstraction cannot see.
    -- `dsimp` exposed the codeword in `hy` as its `Classical.choose` body, while `h_eq`
    -- carries the folded `UDRCodeword` head — unfold it so the rewrite pattern matches.
    unfold UDRCodeword at h_eq
    rw [← h_eq] at hy
    simpa [disagreementSet] using hy
  simpa [fiberwiseDisagreementSet, h_steps_ne, h_exists]

open Classical in
/-- **Definition 4.20** Bad event for folding : This event captures two scenarios where the
random folding challenges undermine the protocol's soundness checks.
For `i ∈ {0, ..., ℓ - steps}`,
- In case `d⁽ⁱ⁾(f⁽ⁱ⁾, C⁽ⁱ⁾) < dᵢ₊steps / 2` (fiberwise close):
  `Δ⁽ⁱ⁾(f⁽ⁱ⁾, f̄⁽ⁱ⁾) ⊄ Δ(fold(f⁽ⁱ⁾, rᵢ', ..., r_{i+steps-1}'),`
  `fold(f̄⁽ⁱ⁾, rᵢ', ..., r_{i+steps-1}'))`,
  i.e. fiberwiseDisagreementSet ⊄ foldedDisagreementSet
- In case `d⁽ⁱ⁾(f⁽ⁱ⁾, C⁽ⁱ⁾) ≥ dᵢ₊steps / 2`  (fiberwise far):
  `d(fold(f⁽ⁱ⁾, rᵢ', ..., rᵢ₊steps₋₁'), C⁽ⁱ⁺steps⁾) < dᵢ₊steps / 2`, i.e. foldedUDRClose
-/
def foldingBadEvent (i : Fin r) {destIdx : Fin r} (steps : ℕ)
    (h_destIdx : destIdx = i + steps) (h_destIdx_le : destIdx ≤ ℓ) [NeZero steps]
  (f_i : OracleFunction 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate) i)
  (r_challenges : Fin steps → L) : Prop :=
  if h_close : fiberwiseClose 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
      (i := i) (steps := steps) (destIdx := destIdx)
      (h_destIdx := by omega) (h_destIdx_le := h_destIdx_le) (f := f_i) then
    let f_bar_i := UDRCodeword 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
      (i := i) (h_i := by omega) (f := f_i)
      (h_within_radius :=
        UDRClose_of_fiberwiseClose 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
          (i := i) (steps := steps) (destIdx := destIdx)
          (h_destIdx := by omega) (h_destIdx_le := h_destIdx_le) f_i h_close)
    let folded_f_i := iterated_fold 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
      (i := i) (steps := steps) (destIdx := destIdx)
      (h_destIdx := by omega) (h_destIdx_le := h_destIdx_le) f_i r_challenges
    let folded_f_bar_i := iterated_fold 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
      (i := i) (steps := steps) (destIdx := destIdx)
      (h_destIdx := by omega) (h_destIdx_le := h_destIdx_le) f_bar_i r_challenges
    ¬ (fiberwiseDisagreementSetPerFiber 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
        (i := i) (steps := steps) (destIdx := destIdx)
        (h_destIdx := by omega) (h_destIdx_le := h_destIdx_le) f_i f_bar_i ⊆
      disagreementSet 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
        (i := destIdx) (destIdx := destIdx) (h_destIdx := rfl)
        (f := folded_f_i) (g := folded_f_bar_i))
  else
    let folded_f_i := iterated_fold 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
      (i := i) (steps := steps) (destIdx := destIdx)
      (h_destIdx := by omega) (h_destIdx_le := h_destIdx_le) f_i r_challenges
    UDRClose 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
      (i := destIdx) (h_i := h_destIdx_le) (f := folded_f_i)

open Classical in
/-- **Definition 4.20.2** (Incremental Bad Events extending Definition 4.20).
For block start index `block_start_idx`, block size `ϑ`, and **partial step count**
`k ≤ ϑ`, with `destIdx = block_start_idx + ϑ` (the block destination),
`E(block_start_idx, k)` is defined as follows:

- If `k = 0`: Returns `False` (no challenges consumed yet).
- Case 1 (fiberwise close at block level):
  `Δ⁽ⁱ⁾(f, f̄) ⊄ Δ⁽ⁱ⁺ᵏ⁾(fold_k(f), fold_k(f̄))`
  where both sides are projected to `S^{i+ϑ}` (the block destination).
- Case 2 (fiberwise far at block level):
  `d⁽ⁱ⁺ᵏ⁾(fold_k(f), C⁽ⁱ⁺ᵏ⁾) < d_{i+ϑ}/2`
  where `d⁽ⁱ⁺ᵏ⁾` is the fiberwise distance projected to `S^{i+ϑ}`.

When `k = ϑ`, this coincides with `foldingBadEvent`. -/
def incrementalFoldingBadEvent
    (block_start_idx : Fin r) {midIdx destIdx : Fin r} (k : ℕ)
    (h_k_le : k ≤ ϑ) (h_midIdx : midIdx = block_start_idx + k)
    (h_destIdx : destIdx = block_start_idx + ϑ) (h_destIdx_le : destIdx ≤ ℓ)
    (f_block_start : OracleFunction 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate) block_start_idx)
    (r_challenges : Fin k → L) : Prop :=
  if h_zero : k = 0 then
    False
  else if h_done : k = ϑ then
    foldingBadEvent 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
      (i := block_start_idx) (steps := ϑ) (destIdx := destIdx)
      (h_destIdx := h_destIdx) (h_destIdx_le := h_destIdx_le)
      f_block_start (fun j : Fin ϑ => r_challenges (Fin.cast h_done.symm j))
  else if h_block_close : fiberwiseClose 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
      (i := block_start_idx) (steps := ϑ) (destIdx := destIdx)
      (h_destIdx := h_destIdx) (h_destIdx_le := h_destIdx_le) (f := f_block_start) then
    let f_bar_block_start := UDRCodeword 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
      (i := block_start_idx) (h_i := by omega) (f := f_block_start)
      (h_within_radius :=
        UDRClose_of_fiberwiseClose 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
          (i := block_start_idx) (steps := ϑ) (destIdx := destIdx)
          (h_destIdx := h_destIdx) (h_destIdx_le := h_destIdx_le)
          f_block_start h_block_close)
    let Δ_fiber := fiberwiseDisagreementSetPerFiber 𝔽q β
      (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
      (i := block_start_idx) (steps := ϑ) (destIdx := destIdx)
      (h_destIdx := h_destIdx) (h_destIdx_le := h_destIdx_le)
      f_block_start f_bar_block_start
    let fold_k_f := iterated_fold 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
      (i := block_start_idx) (steps := k) (destIdx := midIdx)
      (h_destIdx := by omega) (h_destIdx_le := by omega)
      f_block_start r_challenges
    let fold_k_f_bar := iterated_fold 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
      (i := block_start_idx) (steps := k) (destIdx := midIdx)
      (h_destIdx := by omega) (h_destIdx_le := by omega)
      f_bar_block_start r_challenges
    ¬ (Δ_fiber ⊆
      fiberwiseDisagreementSetPerFiber 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
        (i := midIdx) (steps := ϑ - k) (destIdx := destIdx)
        (h_destIdx := by omega) (h_destIdx_le := h_destIdx_le)
        fold_k_f fold_k_f_bar)
  else
    haveI : NeZero (ϑ - k) := ⟨by omega⟩
    let fold_k_f := iterated_fold 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
      (i := block_start_idx) (steps := k) (destIdx := midIdx)
      (h_destIdx := by omega) (h_destIdx_le := by omega)
      f_block_start r_challenges
    fiberwiseClose 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate)
      (i := midIdx) (steps := ϑ - k) (destIdx := destIdx)
      (h_destIdx := by omega) (h_destIdx_le := h_destIdx_le) (f := fold_k_f)

/-- When all folding steps have been applied (`k = ϑ`), the incremental bad event
coincides with the full `foldingBadEvent`. -/
@[simp]
lemma incrementalFoldingBadEvent_of_k_eq_0_is_false
    (block_start_idx : Fin r) (k : ℕ) (h_k : k = 0) {midIdx destIdx : Fin r}
    (h_midIdx : midIdx.val = block_start_idx.val) (h_destIdx : destIdx = block_start_idx + ϑ)
    (h_destIdx_le : destIdx ≤ ℓ)
    (f_block_start : OracleFunction 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate) block_start_idx)
    (r_challenges : Fin k → L) :
    ¬(incrementalFoldingBadEvent 𝔽q β (block_start_idx := block_start_idx) (ϑ := ϑ)
      (k := k) (h_k_le := by omega) (midIdx := midIdx) (destIdx := destIdx)
      (h_midIdx := by omega) (h_destIdx := h_destIdx) (h_destIdx_le := h_destIdx_le)
      f_block_start r_challenges) := by
  simp [incrementalFoldingBadEvent, h_k]

/-- When all folding steps have been applied (`k = ϑ`), the incremental bad event
coincides with the full `foldingBadEvent`. -/
lemma incrementalFoldingBadEvent_eq_foldingBadEvent_of_k_eq_ϑ
    (block_start_idx : Fin r) {midIdx destIdx : Fin r}
    (h_midIdx : midIdx.val = destIdx.val) (h_destIdx : destIdx = block_start_idx + ϑ)
    (h_destIdx_le : destIdx ≤ ℓ)
    (f_block_start : OracleFunction 𝔽q β (h_ℓ_add_R_rate := h_ℓ_add_R_rate) block_start_idx)
    (r_challenges : Fin ϑ → L) :
    incrementalFoldingBadEvent 𝔽q β (block_start_idx := block_start_idx) (k := ϑ)
      (h_k_le := le_refl ϑ)
      (midIdx := midIdx) (destIdx := destIdx)
      (h_midIdx := by omega) (h_destIdx := h_destIdx) (h_destIdx_le := h_destIdx_le)
      f_block_start r_challenges ↔
    foldingBadEvent 𝔽q β (i := block_start_idx) (steps := ϑ)
      (h_destIdx := h_destIdx) (h_destIdx_le := h_destIdx_le)
      f_block_start r_challenges := by
  have hϑ : ϑ ≠ 0 := NeZero.ne ϑ
  simp [incrementalFoldingBadEvent, hϑ, Fin.cast_refl]

end SoundnessTools
end Binius.BinaryBasefold
