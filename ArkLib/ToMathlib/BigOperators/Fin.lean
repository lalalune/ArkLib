/-
Copyright (c) 2026 ArkLib Contributors. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: ArkLib Contributors
-/
import Mathlib.Algebra.BigOperators.Fin

/-!
# Algebraic properties of interval-restricted products over finite domains

This module establishes additional algebraic properties of big operators (products and sums)
evaluated over interval subsets of `Fin (n + 1)`. Specifically, we formalize the compatibility
of products over intervals of the form `Iio i`, `Iic i`, `Ici i` with the algebraic operations
of restriction, extension, and successor mappings. These lemmas serve as core arithmetic
machinery for inductive arguments over finite index structures.
-/

namespace Fin

section Interval

open Finset

variable {M : Type*} [CommMonoid M] {n : ℕ} {v : Fin (n + 1) → M}

@[to_additive]
theorem prod_Iio_succ (i : Fin n) :
    ∏ j ∈ Iio i.succ, v j = (∏ j ∈ Iio i.castSucc, v j) * v i.castSucc := by
  calc
    _ = ∏ j ∈ Ico 0 i.succ, v j :=
      prod_congr (by rw [← bot_eq_zero, Ico_bot]) (fun _ _ => rfl)
    _ = ∏ j ∈ Icc 0 i.castSucc, v j :=
      prod_congr rfl (fun _ _ => rfl)
    _ = ∏ j ∈ insert i.castSucc (Ico 0 i.castSucc), v j :=
      prod_congr (by simp only [zero_le, Ico_insert_right]) (fun _ _ => rfl)
    _ = v i.castSucc * ∏ j ∈ Iio i.castSucc, v j :=
      prod_insert (by simp only [mem_Ico, zero_le, lt_self_iff_false, and_false, not_false_eq_true])
    _ = (∏ j ∈ Iio i.castSucc, v j) * v i.castSucc := mul_comm _ _

@[to_additive]
theorem prod_Iio_eq_univ (i : Fin (n + 1)) :
    ∏ j ∈ Iio i, v j = ∏ j : Fin i, v (Fin.castLE i.isLt.le j) := by
  induction i using Fin.induction with
  | zero =>
    conv_lhs => rw [← bot_eq_zero]
    simp only [Iio_bot, prod_empty, val_zero, univ_eq_empty]
  | succ i hi =>
    rw [prod_Iio_succ, hi]
    change (∏ j : Fin ↑i, v (castLE _ j)) * v i.castSucc = ∏ j : Fin (↑i + 1), v (castLE _ j)
    rw [Fin.prod_univ_castSucc]; congr 1

@[to_additive (attr := simp)]
theorem prod_Iic_zero : ∏ j ∈ Iic 0, v j = v 0 := by
  rw [← bot_eq_zero, Iic_bot, prod_singleton]

@[to_additive]
theorem prod_Iic_succ (i : Fin n) :
    ∏ j ∈ Iic i.succ, v j = (∏ j ∈ Iic i.castSucc, v j) * v i.succ := by
  calc
    _ = ∏ j ∈ Icc 0 i.succ, v j :=
      prod_congr (by rw [← bot_eq_zero, Icc_bot]) (fun _ _ => rfl)
    _ = ∏ j ∈ insert i.succ (Ico 0 i.succ), v j :=
      prod_congr (by simp only [zero_le, Ico_insert_right]) (fun _ _ => rfl)
    _ = v i.succ * ∏ j ∈ Iic i.castSucc, v j :=
      prod_insert (by simp only [mem_Ico, zero_le, lt_self_iff_false, and_false, not_false_eq_true])
    _ = (∏ j ∈ Iic i.castSucc, v j) * v i.succ := mul_comm _ _

@[to_additive]
theorem prod_Iic_eq_univ (i : Fin (n + 1)) :
    ∏ j ∈ Iic i, v j = ∏ j : Fin (i + 1), v (Fin.castLE i.isLt j) := by
  induction i using Fin.induction with
  | zero => simp only [prod_Iic_zero, val_zero, Nat.reduceAdd, univ_unique, default_eq_zero,
    prod_singleton]; exact congrArg v (Fin.ext rfl)
  | succ i hi =>
    rw [prod_Iic_succ, hi]
    change (∏ j : Fin (↑i + 1), v (castLE _ j)) * v i.succ =
      ∏ j : Fin (↑i + 1 + 1), v (castLE _ j)
    conv_rhs => rw [Fin.prod_univ_castSucc]
    congr 1

@[simp]
theorem Ici_zero : Ici (0 : Fin (n + 1)) = univ := by rw [← bot_eq_zero, Ici_bot]

@[simp]
theorem Iio_zero : Iio (0 : Fin (n + 1)) = ∅ := by rw [← bot_eq_zero, Iio_bot]

@[simp]
theorem Iic_zero : Iic (0 : Fin (n + 1)) = {0} := by rw [← bot_eq_zero, Iic_bot]

theorem Iic_castSucc (i : Fin n) : Iic (castSucc i) = (Iic i).map Fin.castSuccEmb := by
  rw [Iic_eq_cons_Iio, Iic_eq_cons_Iio, map_cons]
  simp only [Iio_castSucc, cons_eq_insert, castSuccEmb_apply]

@[simp]
theorem Ici_succ (i : Fin n) : Ici i.succ = (Ici i).map (Fin.succEmb _) := by
  rw [Ici_eq_cons_Ioi, Ici_eq_cons_Ioi, map_cons]
  simp only [Ioi_succ, cons_eq_insert, coe_succEmb]

end Interval

end Fin
